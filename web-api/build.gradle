buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '3.2.0')
    }
}

plugins {
    id "org.springframework.boot" version "1.5.1.RELEASE"
    id "fi.evident.apina" version "0.10.7"
    id "org.sonarqube" version "2.6.2"
    id "org.owasp.dependencycheck" version "4.0.2"
}

apply plugin: 'com.jfrog.artifactory'

artifactoryPublish.skip = true

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()

    // put your artifactory parameters to $HOME/.gradle/gradle.properties
    if (project.hasProperty('artifactoryUrl')) {
        maven {
            url "$artifactoryUrl"
            credentials {
                username "$artifactoryUsername"
                password "$artifactoryPassword"
            }
        }
    }
}

configurations {
    compile.exclude module: 'commons-logging'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Dalston.SR5"
    }
}

dependencies {

    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-devtools"
    compile 'org.springframework.boot:spring-boot-starter-jdbc'
    compile "org.springframework.boot:spring-boot-starter-tomcat"

    compile "org.springframework:spring-webmvc"
    compile "org.springframework:spring-tx"
    compile "org.springframework:spring-jdbc"
    compile "org.springframework:spring-context-support"

    compile "org.springframework.security:spring-security-core"
    compile "org.springframework.security:spring-security-config"
    compile "org.springframework.security:spring-security-web"
    compile "fi.vm.yti:yti-spring-security:0.1.10"

    compile "io.jsonwebtoken:jjwt:0.9.1"

    compile "org.postgresql:postgresql:9.4.1212"
    compile "org.dalesbred:dalesbred:1.2.5"
    compile "org.flywaydb:flyway-core:3.2.1"

    compile "com.fasterxml.jackson.core:jackson-databind"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8"

    compile "org.jetbrains:annotations:15.0"

    compile "org.apache.httpcomponents:httpclient:4.5.2"

    compile "org.thymeleaf:thymeleaf"
    compile "org.thymeleaf:thymeleaf-spring4"

    compile "io.springfox:springfox-swagger2:2.7.0"
    compile "io.springfox:springfox-swagger-ui:2.7.0"

    compile group: 'javax.mail', name: 'mail', version: '1.4'
    compile group: 'javax.mail', name: 'javax.mail-api', version: '1.5.1'
    compile "org.springframework.boot:spring-boot-starter-mail"

    compile 'org.springframework.cloud:spring-cloud-starter-config'

    compile('org.springframework.cloud:spring-cloud-starter-sleuth')
    compileOnly "org.springframework.boot:spring-boot-configuration-processor"

    compile "ch.qos.logback:logback-core"
    compile "net.logstash.logback:logstash-logback-encoder:5.0"

    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "junit:junit:4.12"
    testCompile "org.springframework:spring-test"
    testCompile "com.jayway.jsonpath:json-path:2.0.0"
    testCompile "org.hsqldb:hsqldb:2.3.4"
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.11'

    compile project(':frontend')
}

jar {
    baseName = 'yti-groupmanagement'
    dependsOn tasks.findByPath(":frontend:frontendJar")

    from zipTree(tasks.findByPath(":frontend:frontendJar").outputs.files.singleFile)
}

springBoot {
    executable = true
    backupSource = false
}

bootRepackage {
    mainClass = 'fi.vm.yti.groupmanagement.Main'
}

bootRun {
	systemProperties System.properties
}

sonarqube {
    properties {
        property "sonar.projectKey", "yti-groupmanagement-web-api"
        property "sonar.projectName", "yti-groupmanagement-web-api"
        property("sonar.dependencyCheck.reportPath", "$buildDir/reports/dependency-check-report.xml")
        property("sonar.dependencyCheck.htmlReportPath", "$buildDir/reports/dependency-check-report.html")
    }
}

dependencyCheck {
    format = 'ALL'
    autoUpdate = false
    analyzers {
        centralEnabled = false
    }
    data {
        directory = System.getenv('OWASP_DATA_DIR')
    }
}

apina {
    target = new File(project(":frontend").projectDir, 'src/app/apina.ts')
    blackBoxClasses = [/org\.dalesbred\..+/, /java\.time\..+/]
    enumMode = 'STRING_UNION'
}

tasks.findByPath(":frontend:installDependencies").dependsOn apina
